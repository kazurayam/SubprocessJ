/**
 * How to publish the subprocessj artifacts?
 *
 * $ gradle publish -Psigning.password=########
 *
 * here ######## must be interpolated with the passphrase which is used to protect kazurayam's secret key
 */

plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
    mavenLocal()
}

group = 'com.kazurayam'
archivesBaseName = "subprocessj"
version = '0.2.1'

ext.isReleaseVersion = ! version.endsWith("SNAPSHOT")

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

test {
    useJUnitPlatform()
    maxParallelForks = 2
    finalizedBy jacocoTestReport
}



javadoc {
    options.encoding = 'UTF-8'
    options.locale = "en_US"
}
task copyJavadoc(type: Copy) {
    from 'build/docs/javadoc'
    into 'docs/api'
}
javadoc.finalizedBy copyJavadoc

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                groupId = project.group
                name = project.rootProject.name
                description = 'an easy-to-use wrapper of java.lang.ProcessBuilder'
                url = 'https://kazurayam.github.io/subprocessj/'
                from components.java
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org.licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kazurayam'
                        name = 'URAYAMA,kazuaki'
                        email = 'kazuaki.urayama@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/kazurayam/${project.rootProject.name}.git"
                    developerConnection = "scm:git:git@github.com:kazurayam/${project.rootProject.name}.git"
                    url = "https://github.com/kazurayam/${project.rootProject.name}"
                }
            }
        }
    }
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo: snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
}


signing {
    sign publishing.publications.mavenJava
}
// I do not want to sign the SNAPSHOT
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

